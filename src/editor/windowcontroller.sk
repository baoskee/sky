namespace Editor {
  interface WindowObserver {
    def handleAction(action Action)
    def handleFrame
  }

  class WindowController :: UI.WindowDelegate, DocumentStoreObserver {
    const _window UI.Window
    const _observers List<WindowObserver> = []
    const _documentStore = DocumentStore.new
    const _view View

    def new(platform UI.Platform) {
      var window = platform.createWindow
      window.setTitle("Untitled - Sky Text Editor")
      window.setTheme(.XCODE)
      window.setFont(.CODE_FONT, MONOSPACE_FONTS, 12, 16, 0)
      window.setFont(.MARGIN_FONT, MONOSPACE_FONTS, 10, 14, 0)
      window.setFont(.UI_FONT, SANS_SERIF_FONTS, 12, 16, 0)
      _window = window

      _view = View.new(self)
      if BUILD != .TERMINAL {
        _view.changePadding(5, 5, 5, 5)
        _view.changeMarginPadding(15, 5)
        _view.setLayout(.STRETCH, 0, 0, .STRETCH, TabsView.HEIGHT, 0)

        var tabs = TabsView.new(self)
        tabs.setLayout(.STRETCH, 0, 0, .MIN, 0, TabsView.HEIGHT)
        tabs.appendTo(window.root)
      } else {
        _view.setScrollbarThickness(1)
        _view.changePadding(0, 0, 1, 0) # Leave one character of padding on the right so the caret can be seen at the end of the line
        _view.changeMarginPadding(1, 1)
      }
      _view.appendTo(window.root)

      _documentStore.addObserver(self)
      _documentStore.appendDocument(createNewDocument)

      window.setDelegate(self)
      window.focusView(_view)
    }

    def window UI.Window {
      return _window
    }

    def documentStore DocumentStore {
      return _documentStore
    }

    def createNewDocument Model {
      var document = Model.new("Untitled", window.platform, window.renderer.fontInstance(.CODE_FONT), 2)
      document.setLexer(.SKEW)
      return document
    }

    def addObserver(observer WindowObserver) {
      _observers.appendOne(observer)
    }

    def removeObserver(observer WindowObserver) {
      _observers.removeOne(observer)
    }

    def triggerFrame {
      for observer in _observers {
        observer.handleFrame
      }
    }

    def triggerAction(action Action) {
      for observer in _observers {
        observer.handleAction(action)
      }
    }

    def handleDocumentStoreChange(active Model, moved Model, from int, to int) {
      _view.setModel(active)
      _window.invalidate
    }
  }

  namespace WindowController {
    const MONOSPACE_FONTS = [
      "Monaco",
      "Menlo",
      "Consolas",
      "Courier New",
      "monospace",
    ]

    const SANS_SERIF_FONTS = [
      "San Francisco",
      "Lucida Grande",
      "Segoe UI",
      "Arial",
      "sans-serif",
    ]
  }
}
